' Gambas module file

Private Contenido As Result
Private NombreProyecto As String
Private RutaProyecto As String

Public Sub CerrarTodo()

  ' Borramos carpetas del caché
  FMain.TerminalViewProyecto.Input("rm -rf " & User.Home & "/.local/share/org.gambas.*" & "\n")
  FMain.TerminalViewProyecto.Input("clear" & "\n")
  Wait 0.2

  ' detiene el proceso Bash
  If FMain.$Bash And If FMain.$Bash.State = Process.Running Then
    FMain.$Bash.Kill
    Wait 0.2
    If FMain.$Bash.State = Process.Running Then
      FMain.$Bash.Terminate
      Wait 0.2
      If FMain.$Bash.State = Process.Running Then
        Message.Warning("El proceso no pudo ser detenido.")
      Endif
    Endif
  Endif

  ' Cierra la conexión a la BBDD si existe
  If m_OnOff_y_Red.meConn Then
    m_OnOff_y_Red.meConn.Close()
    m_OnOff_y_Red.meConn = Null
  Endif

  ' Finalmente cerrar formulario principal y salir de la aplicación
  FMain.Close
  Quit

End

Public Sub DirectorioOcultoApp()

  ' Lista de rutas que necesitas verificar/crear
  Dim rutasNecesarias As String[] = [
    User.Home & "/.gbadoc",
    User.Home & "/.gbadoc/csl",
    User.Home & "/.gbadoc/xslt",
    User.Home & "/.gbadoc/ltx"

  ]

  ' Verificar y crear rutas si no existen
  For Each rutaDirSys As String In rutasNecesarias
    If Not Exist(rutaDirSys) Then
      Try Mkdir rutaDirSys  ' ← Intenta crear el directorio
      If Error Then
        Message.Error("Error al intentar crear los directorios del sistema.")' Print "Error al crear directorio: " & rutaDirSys & " (" & Error.Text & ")"
      Endif
    End If
  Next

  'copiamos los archivos de configuracion general
  If Not File.RealPath(User.Home & "/.gbadoc/gbadoc.sqlite") Then
    Message.Info("La base de datos no existe, se creará una vacía.")
    Copy "vacia-gbadoc.sqlite" To User.Home & "/.gbadoc/gbadoc.sqlite"
  End If
  If Not File.RealPath(User.Home & "/.gbadoc/filtro.lua") Then
    Copy "filtro.lua" To User.Home & "/.gbadoc/filtro.lua"
  End If
  If Not File.RealPath(User.Home & "/.gbadoc/csl/apa.csl") Then
    Copy "apa.csl" To User.Home & "/.gbadoc/csl/apa.csl"
  End If
  If Not File.RealPath(User.Home & "/.gbadoc/csl/vancouver.csl") Then
    Copy "vancouver.csl" To User.Home & "/.gbadoc/csl/vancouver.csl"
  End If
  If Not File.RealPath(User.Home & "/.gbadoc/csl/ieee.csl") Then
    Copy "ieee.csl" To User.Home & "/.gbadoc/csl/ieee.csl"
  End If

End

Public Sub VerificarYAgregarProyecto(sRutaArchivo As String)

  Dim sNombreArchivo As String
  Dim sSQL As String

  ' Asegurar conexión activa
  If Not m_OnOff_y_Red.meConn Or If m_OnOff_y_Red.meConn.Opened = False Then
    m_OnOff_y_Red.OnRed()
  Endif

  ' Obtener nombre del archivo sin extensión
  sNombreArchivo = File.BaseName(sRutaArchivo)

  ' Verificar si ya existe
  sSQL = "SELECT id FROM revistas WHERE nombre_archivo = &1"
  Contenido = m_OnOff_y_Red.meConn.Exec(sSQL, sNombreArchivo)

  If Contenido.Available Then
    Message.Info("El archivo '" & sNombreArchivo & "' ya está registrado.")
  Else
    ' Insertar en revistas
    sSQL = "INSERT INTO revistas (nombre_archivo) VALUES (&1)"
    m_OnOff_y_Red.meConn.Exec(sSQL, sNombreArchivo)

    Message.Info("Proyecto registrado con metadatos iniciales.")
  Endif

Catch
  Message.Error("Error al registrar el proyecto: " & Error.Text)

End
